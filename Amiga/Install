(set #prefix "PETSCIIRobots")

(set #dest
  (askdir
    (prompt
      (cat
	"Where should \"" @app-name "\" be installed?\n"
	"A drawer \"" #prefix "\" will be automatically created."
      )
    )
    (help @askdir-help)
    (default @default-dest)
  )
)

(set @default-dest #dest)
(set #dest (tackon #dest #prefix))
(set #dest-org #dest)
(set #dest-data (tackon #dest "Data"))

(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt
          (cat
            "\nDirectory \"" #dest "\" already exists.\n"
            "Should it be deleted?"
          )
        )
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)

(copyfiles
  (help @copyfiles-help)
  (source "AmigaRobots")
  (dest #dest)
  (infos))

(copyfiles
  (help @copyfiles-help)
  (source "PETRobots")
  (dest #dest)
  (infos))

(makedir
  (help @makedir-help)
  #dest-data)

(working "Decompressing data files")

(foreach
  "Data"
  "#?.gz"
  ((set #source-file (tackon "Data" @each-name))
   (set #dest-file (tackon #dest-data (substr @each-name 0 (- (strlen @each-name) 3))))
   (working "Decompressing " #source-file " to " #dest-file)
   (run ("C/DecompressData \"%s\" \"%s\"" #source-file #dest-file))))
